plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.flywaydb.flyway' version '9.19.3'
	id 'org.jooq.jooq-codegen-gradle' version "3.19.15"
	id 'org.jlleitschuh.gradle.ktlint' version "11.3.1"
}

group = 'com.quo-card'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sourceSets {
	main {
		java {
			srcDir("build/generated-src/jooq/main")
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starter Dependencies
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'

	// Database and Flyway
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	runtimeOnly 'org.postgresql:postgresql'

	// Kotlin
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

	// OpenAPI Documentation
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

	// jOOQ
	implementation 'org.jooq:jooq:3.19.5'
	implementation 'org.jooq:jooq-meta:3.19.5'
	implementation 'org.jooq:jooq-codegen:3.19.5'
	jooqCodegen 'org.postgresql:postgresql'

	// Bean Validation
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.glassfish:jakarta.el:4.0.2'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

	// Development
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.5'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core:5.11.0'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/postgres'
	user = 'postgres'
	password = 'postgres'
	schemas = ['public']
	locations = ['filesystem:src/main/resources/db/migration']
	cleanDisabled = false
}

jooq {
	configuration {
		jdbc {
			driver = 'org.postgresql.Driver'
			url = 'jdbc:postgresql://localhost:5432/postgres'
			user = 'postgres'
			password = 'postgres'
		}
		generator {
			name = 'org.jooq.codegen.DefaultGenerator'
			database {
				name = 'org.jooq.meta.postgres.PostgresDatabase'
				inputSchema = 'public'
			}
			target {
				packageName = 'com.quoCard.bookManagementApp.jooq'
				directory = "$buildDir/generated-src/jooq/main"
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}